package cc.catalysts.gradle.systemjs.task

import cc.catalysts.gradle.systemjs.SystemjsExtension
import org.gradle.api.DefaultTask
import org.gradle.api.tasks.TaskAction

/**
 * @author Thomas Scheinecker, Catalysts GmbH
 */
class CreateSystemjsWebjarConfig extends DefaultTask {
    @TaskAction
    void createSystemjsWebjarConfig() {
        SystemjsExtension config = project.systemjs;

        Map<String, String> webjarPaths = [:];

        project.configurations.forEach({ configuration ->
            configuration.dependencies.findAll { it.group.startsWith('org.webjars') } forEach {
                webjarPaths.put(it.name, "webjars/${it.name}/${it.version}")
            }

        });

        StringBuilder sb = new StringBuilder()

        for (Map.Entry<String, String> entry : webjarPaths) {
            if (sb.size() != 0) {
                sb.append ",\n        "
            }
            sb.append "'$entry.key': '$entry.value'"
        }

        String webjarConfigContent = """// Generated by cat-boot-ui-gradle plugin
System.config({
    map: {
        ${sb}
    }
});
"""

        File webjarConfig = new File(config.destinationDir, 'webjar-config.js')
        webjarConfig.parentFile.mkdirs()
        webjarConfig.write webjarConfigContent
    }
}
