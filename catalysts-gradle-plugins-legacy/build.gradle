description = 'collection of gradle plugins'
group = 'cc.catalysts.gradle'
version = '2.3'

if (hasProperty('teamcity')) {
    version += property('teamcity')['build.number']
    logger.lifecycle "##teamcity[buildNumber '" + version + "']"
}

apply plugin: 'groovy'
apply plugin: 'idea'

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}

repositories {
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
}

configurations {
    snapshot.extendsFrom(archives)
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile "org.sonarqube.gradle:gradle-sonarqube-plugin:1.1"

    // cat-deploy
    compile 'commons-net:commons-net:3.2'
    compile 'com.jcraft:jsch:0.1.49'

    // cat-grails
    compile 'com.connorgarvey.gradle:gradle-grails-wrapper:1.0.1'

	// cat-jdeps
	runtime 'commons-lang:commons-lang:2.3'

    // cat-less
    compile 'org.lesscss:lesscss:1.3.1'
    // seems like dependencies of org.lesscss:lesscss won't get resolved, so they 're included here:
    runtime 'org.mozilla:rhino:1.7R4'
    runtime 'commons-io:commons-io:2.4'

    // cat-sass
    runtime group: 'org.jruby', name: 'jruby-complete', version: '1.6.8'
    runtime group: 'me.n4u.sass', name: 'sass-gems', version: '3.2.1'

    // cat-webdeploy
    runtime group: 'org.apache.ant', name: 'ant-jsch', version: '1.9.2'

    // explicitly needed or it will download both each time the gwt plugin is executed
    testCompile  'com.google.gwt:gwt-dev:2.4.0'
    testCompile  'com.google.gwt:gwt-user:2.4.0'
}

uploadSnapshot {
    repositories {
        ivy {
            credentials {
                username = System.getenv('IVY_USERNAME')
                password = System.getenv('IVY_PASSWORD')
            }
            url System.getenv('IVY_HOST')
            layout 'maven'
        }
    }
}
